tree grammar LatteTreeBuilder;
/*------------------------------------------------------------------
 * Artur Kozak [320770], MRJP 2014 zadanie 2
 * Gramatyka ANTLR3 zamieniajaca AST wygenerowane przez parser z Latte.g
 * na drzewo obiektow LatteTree do dalszego przetwarzania.
 *------------------------------------------------------------------*/

options {
    language = Python;
    tokenVocab = Latte;
    ASTLabelType = CommonTree;
}

// Header pasted on the top of parser file.
@header {
from FuturePrint import debug
from LatteLexer import LatteLexer
from LatteParser import LatteParser
import LatteParser as LP
from LatteErrors import Status
from LatteNodes import *
from LatteUtils import FunArg, DeclArg
}

@members {
}

@main {
def main(argv):
    """ Main function in case the parser is executed directly.
    
    Only for testing purposes. Reads code from stdin, writes diagnostic messages
    and textual representation of the tree, function and symbol names. """
    lexer = LatteLexer(ANTLRInputStream(sys.stdin))
    tokens = CommonTokenStream(lexer)
    Status.setTokenStream(tokens)
    parser = LatteParser(tokens)
    prog = parser.prog()
    debug("-----------------------------------------------");
    debug("Tree: ", prog.tree.toStringTree())
    debug("-----------------------------------------------");
    nodes = CommonTreeNodeStream(prog.tree)
    nodes.setTokenStream(tokens)
    Status.setNodeStream(nodes)
    walker = LatteTreeBuilder(nodes)
    root = walker.prog()
    debug("functions: \%d" \% len(root.children))
    debug("symbols: \%d" \% len(root.symbols))
}

/*------------------------------------------------------------------
 * reguly parsera AST
 *------------------------------------------------------------------
 * From each node of the AST generated by LatteParser, a node subclassing
 * LatteNode is created, and its necessary attributes are set.
 * In particular, in some cases the `pos` argument needs to be adjusted
 * to correctly indicate the related token's position in the source code
 * -- in order to properly show possible error messages later.
 *------------------------------------------------------------------*/

// program -------------------------------------------------
prog returns [lt=ProgTree()]
    : ^(PROG (fundef { $lt.addFunTree($fundef.lt); } )* )
    ;

fundef returns [lt=FunTree()]
    : ^(FUNDEF
            type { $lt.setRetType($type.id); }
            IDENT { $lt.setName($IDENT.text); }
            (arg { $lt.addArg($arg.fa); })*
            block { $lt.setBlock($block.lt); }
       )
    ;

arg returns [fa]
    : ^(ARG type IDENT) { $fa = FunArg($type.id, $IDENT.text); }
    ;

type returns [id]
    : t=(INT | STRING | BOOLEAN | VOID) { $id = $t.type; }
    ;

block returns [lt=BlockTree()]
    : ^(BLOCK (stmt { $lt.addStmt($stmt.lt); })* )
    ;

// statements ----------------------------------------------
stmt returns [lt=StmtTree()]
    : block
        { $lt = $block.lt; }
    | ^(DECL type { $lt = DeclTree($type.id); }
            (ditem { $lt.addItem($ditem.item); })+
       )
    | ^(ASSIGN { $lt = StmtTree(ASSIGN); } 
            IDENT { $lt.addChild(LiteralTree(IDENT, $IDENT.text)); }
            expr { $lt.addChild($expr.lt); }
       )
    | ^(op=(INCR|DECR) IDENT)
        { $lt = StmtTree($op.type, children=[LiteralTree(IDENT, $IDENT.text, pos_off=-2)]); }
    | ^(RETURN { $lt = StmtTree(RETURN); }
            (expr { $lt.addChild($expr.lt); })?
       )
    | ^(IF { if_pos = Status.getCurPos() } expr
        { $lt = StmtTree(IF, children=[$expr.lt], pos=if_pos); }
            (s=stmt { $lt.addChild($s.lt); })?
            (ifelse { $lt.addChild($ifelse.lt); })?
       )
    | ^(WHILE expr
            { $lt = StmtTree(LP.WHILE, children=[$expr.lt]); }
            (s=stmt { $lt.addChild($s.lt); })?
       )
    | expr
        { $lt = $expr.lt; }
    ;

ditem returns [item]
    : ^(DITEM IDENT { id_pos = Status.getCurPos() } )
        { $item = DeclArg($IDENT.text, id_pos); }
    | ^(ASSIGN IDENT { id_pos = Status.getCurPos() } expr)
        { $item = DeclArg($IDENT.text, id_pos, $expr.lt); }
    ;

ifelse returns [lt=StmtTree()]
    : ^(ELSE { e_pos = Status.getCurPos() } stmt { $lt = $stmt.lt; })
        { if $lt.pos == '0:0': $lt.pos = e_pos }
    ;

// expressions ---------------------------------------------
expr returns [lt=ExprTree]
    : lit=(IDENT|NUMBER|STRINGLIT|TRUE|FALSE)
        { $lt = LiteralTree($lit.type, $lit.text); }
    | ^(op=(NOT|NEG) e=expr)
        { $lt = UnopTree($op.type, $e.lt); }
    | ^(op=(MULT|DIV|MOD|PLUS|MINUS | LT|LEQ|GT|GEQ|EQ|NEQ | AND|OR) { op_pos = Status.getCurPos() }
           a=expr b=expr
       )
        { $lt = BinopTree($op.type, $a.lt, $b.lt, pos=op_pos); }
    | ^(FUNCALL IDENT { $lt = FuncallTree($IDENT.text); }
            (e=expr { $lt.addChild($e.lt); } )*
       )
    ;
